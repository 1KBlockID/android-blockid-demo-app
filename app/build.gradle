apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdk 34
    defaultConfig {
        namespace "com.onekosmos.blockidsample"
        applicationId "com.onekosmos.blockid.poc"
        minSdkVersion 26
        targetSdkVersion 34
        versionCode 1
        versionName "1.20.30" + "." + getCurrentTimestampHexValue()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        buildConfig = true
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
        exclude 'lib/x86_64/darwin/libscrypt.dylib'
        exclude 'lib/x86_64/freebsd/libscrypt.so'
        exclude 'lib/x86_64/linux/libscrypt.so'
        exclude 'META-INF/INDEX.LIST'
        exclude 'META-INF/LICENSE.md'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE.md'
        exclude 'AndroidManifest.xml'
    }
}

String getCurrentTimestampHexValue() {
    //get current date of build as hex of (current time in seconds)
    Date date = new Date()
    long timestamp = (date.getTime() / 1000)
    String hexVal = Long.toHexString(timestamp)
    println("HEX VALUE--" + hexVal.toUpperCase())
    return hexVal.toUpperCase()
}

configurations.configureEach {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == 'org.bouncycastle') {
            details.useTarget 'org.bouncycastle:bcprov-jdk15to18:1.70'
        }
    }
}

dependencies {
    //The default toolchain implements the new language features by performing bytecode
    // transformations, called desugar
    coreLibraryDesugaring("com.android.tools:desugar_jdk_libs:1.2.0")

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.navigation:navigation-fragment:2.8.9'
    implementation 'androidx.navigation:navigation-ui:2.8.9'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    // Secure  Shared preference
    implementation 'androidx.security:security-crypto:1.1.0-alpha03'

    implementation 'androidx.biometric:biometric:1.1.0'

    // RecyclerView
    implementation "androidx.recyclerview:recyclerview:1.2.1"

    // Network Call
    implementation 'com.github.amitshekhariitbhu.Fast-Android-Networking:android-networking:1.0.4'

    //Location
    implementation 'com.google.android.gms:play-services-gcm:17.0.0'
    implementation 'com.google.android.gms:play-services-location:20.0.0'

    // LiveID
    implementation 'com.google.guava:guava:31.1-android'

    // kotlin
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.1'

    // Firebase Crashlytics
    // Import the BoM for the Firebase platform
    // When using the BoM, you don't specify versions in Firebase library dependencies
    implementation platform('com.google.firebase:firebase-bom:33.12.0')
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-analytics'

    // BlockID SDK
    implementation 'com.onekosmos.blockid.sdk:blockidsdk:1.20.30.68272E83'
}
