apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdkVersion 'android-UpsideDownCake'
    defaultConfig {
        applicationId "com.onekosmos.blockid.poc"
        minSdkVersion 23
        targetSdkVersion 34
        versionCode 1
        versionName "1.9.30" + "." + getCurrentTimestampHexValue()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
        exclude 'lib/x86_64/darwin/libscrypt.dylib'
        exclude 'lib/x86_64/freebsd/libscrypt.so'
        exclude 'lib/x86_64/linux/libscrypt.so'
        exclude 'META-INF/INDEX.LIST'
        exclude 'META-INF/LICENSE.md'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE.md'
        exclude 'AndroidManifest.xml'
    }
}

String getCurrentTimestampHexValue() {
    //get current date of build as hex of (current time in seconds)
    Date date = new Date()
    long timestamp = (date.getTime() / 1000)
    String hexVal = Long.toHexString(timestamp)
    println("HEX VALUE--" + hexVal.toUpperCase())
    return hexVal.toUpperCase()
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == 'org.bouncycastle') {
            details.useTarget 'org.bouncycastle:bcprov-jdk15to18:1.70'
        }
        def kotlinVersion = "1.8.0"
        def requested = details.requested
        if (requested.group == 'org.jetbrains.kotlin') {
            List<String> list = ['kotlin-stdlib', 'kotlin-stdlib-jdk7', 'kotlin-stdlib-jdk8',
                                 'kotlin-stdlib-common']
            if (list.contains(requested.name)) {
                details.useVersion kotlinVersion
            }
        }
    }
}

dependencies {
    //The default toolchain implements the new language features by performing bytecode
    // transformations, called desugar
    coreLibraryDesugaring("com.android.tools:desugar_jdk_libs:2.0.3")

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.android.material:material:1.10.0-alpha02'
    implementation 'androidx.navigation:navigation-fragment:2.5.3'
    implementation 'androidx.navigation:navigation-ui:2.5.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    implementation 'com.google.code.gson:gson:2.10.1'

    // Secure  Shared preference
    implementation 'androidx.security:security-crypto:1.1.0-alpha06'

    //Qrcode scan + DL
    implementation 'com.google.android.gms:play-services-vision:20.1.3'

    // wallet
    implementation 'org.web3j:core:5.0.0'

    //generate mnemonics
    implementation('org.bitcoinj:bitcoinj-core:0.16.1') {
        exclude group: 'com.google.protobuf', module: 'protobuf-java'
        exclude group: 'net.jcip', module: 'jcip-annotations'
    }

    implementation 'androidx.biometric:biometric:1.1.0'
    implementation 'org.jetbrains:annotations:24.0.1'
    // For Base32 encode/decode
    //noinspection GradleDependency
    implementation 'commons-codec:commons-codec:1.15'

    // RecyclerView
    implementation "androidx.recyclerview:recyclerview:1.3.0"

    // Network Call
    implementation "com.amitshekhar.android:android-networking:1.0.2"

    // passport scan
    implementation('org.jmrtd:jmrtd:0.7.37') {
        exclude module: 'bcprov-jdk15on'
        exclude module: 'bctls-jdk15on'
    }

    implementation 'org.innovatrics.mrz:mrz-java:0.4'

    //Location
    implementation 'com.google.android.gms:play-services-gcm:17.0.0'
    implementation 'com.google.android.gms:play-services-location:21.0.1'

    // nfc scanner
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'net.sf.scuba:scuba-sc-android:0.0.24'
    implementation 'com.madgag.spongycastle:prov:1.58.0.0'
    implementation 'com.github.mhshams:jnbis:2.1.2'
    implementation 'com.gemalto.jp2:jp2-android:1.0'

    //for json array filters
    implementation 'com.annimon:stream:1.2.2'

    // CameraX
    implementation 'androidx.camera:camera-camera2:1.3.0-alpha06'
    implementation 'androidx.camera:camera-lifecycle:1.3.0-alpha06'
    implementation 'androidx.camera:camera-view:1.3.0-alpha06'

    // LiveID
    implementation 'com.google.android.gms:play-services-mlkit-face-detection:17.1.0'
    implementation 'com.google.guava:guava:31.0.1-android'

    // Text recognition
    implementation 'com.google.android.gms:play-services-mlkit-text-recognition:19.0.0'

    // Barcode scanning
    implementation 'com.google.mlkit:barcode-scanning:17.1.0'

    // Opencv
    implementation 'com.quickbirdstudios:opencv:4.5.3.0'

    // for adding custom tab intent
    implementation 'androidx.browser:browser:1.5.0'

    // kotlin
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.1'

    // Firebase Crashlytics
    // Import the BoM for the Firebase platform
    // When using the BoM, you don't specify versions in Firebase library dependencies
    implementation platform('com.google.firebase:firebase-bom:30.3.1')
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-analytics'

    // wallet connect
    implementation("com.walletconnect:sign:2.13.0")
    implementation("com.walletconnect:android-core:1.15.0")
    implementation 'org.web3j:utils:5.0.0'

    // Native FIDO2 External
    implementation 'com.google.android.gms:play-services-fido:20.0.1'
    implementation 'androidx.lifecycle:lifecycle-process:2.6.1'

    // BlockID SDK
    implementation 'com.onekosmos.blockid.sdk:blockidsdk:1.9.30.test64678C1C'
}